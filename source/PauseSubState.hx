package;

import flixel.util.FlxTimer;
import Controls.Control;
import flixel.FlxG;
import flixel.FlxSprite;
import flixel.FlxSubState;
import flixel.addons.transition.FlxTransitionableState;
import flixel.group.FlxGroup.FlxTypedGroup;
import flixel.input.keyboard.FlxKey;
import flixel.sound.FlxSound;
import flixel.text.FlxText;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.util.FlxColor;

using StringTools;

class PauseSubState extends MusicBeatSubstate
{
	var grpMenuShit:FlxTypedGroup<Alphabet>;

	var pauseOG:Array<String> = [
		'Resume',
		'Restart Song',
		'Change Difficulty',
		'Toggle Practice Mode',
		'Exit to menu'
	];
	var difficultyChoices:Array<String> = ['EASY', 'NORMAL', 'HARD', 'BACK'];

	var menuItems:Array<String> = [];
	var curSelected:Int = 0;

	var pauseMusic:FlxSound;

	var songRoleText:FlxText;
	var practiceText:FlxText;

	public function new(x:Float, y:Float)
	{
		super();

		menuItems = pauseOG;

		pauseMusic = new FlxSound().loadEmbedded(Paths.music('breakfast'), true, true);
		pauseMusic.volume = 0;
		pauseMusic.play(false, FlxG.random.int(0, Std.int(pauseMusic.length / 2)));

		FlxG.sound.list.add(pauseMusic);

		var bg:FlxSprite = new FlxSprite().makeGraphic(FlxG.width, FlxG.height, FlxColor.BLACK);
		bg.alpha = 0;
		bg.scrollFactor.set();
		add(bg);

		var levelInfo:FlxText = new FlxText(20, 15, 0, "", 32);
		levelInfo.text += PlayState.SONG.song;
		levelInfo.scrollFactor.set();
		levelInfo.setFormat(Paths.font("vcr.ttf"), 32);
		levelInfo.updateHitbox();
		add(levelInfo);

		var levelDifficulty:FlxText = new FlxText(20, 15 + 32, 0, "", 32);
		levelDifficulty.text += CoolUtil.difficultyString();
		levelDifficulty.scrollFactor.set();
		levelDifficulty.setFormat(Paths.font('vcr.ttf'), 32);
		levelDifficulty.updateHitbox();
		add(levelDifficulty);

		var deathCounter:FlxText = new FlxText(20, 15 + 64, 0, "", 32);
		deathCounter.text = "Blue balled: " + PlayState.deathCounter;
		deathCounter.scrollFactor.set();
		deathCounter.setFormat(Paths.font('vcr.ttf'), 32);
		deathCounter.updateHitbox();
		add(deathCounter);

		songRoleText = new FlxText(20, 15 + 64 + 32, 0, "", 32);
		songRoleText.scrollFactor.set();
		songRoleText.setFormat(Paths.font('vcr.ttf'), 32);
		songRoleText.updateHitbox();
		add(songRoleText);

		practiceText = new FlxText(20, 15 + 64 + 64, 0, "PRACTICE MODE", 32);
		practiceText.scrollFactor.set();
		practiceText.setFormat(Paths.font('vcr.ttf'), 32);
		practiceText.updateHitbox();
		practiceText.x = FlxG.width - (practiceText.width + 20);
		practiceText.visible = PlayState.practiceMode;
		add(practiceText);

		final artistText:String = 'Artist: ' + PlayState.SONG.artist;
		final charterText:String = 'Charter: ' + PlayState.SONG.charter;

		final missingArtist = PlayState.SONG.artist == null || PlayState.SONG.artist.trim().length < 1;
		final missingCharter = PlayState.SONG.charter == null || PlayState.SONG.charter.trim().length < 1;

		if (missingArtist && missingCharter)
		{
			remove(songRoleText);
			practiceText.y -= 32;
		}
		else if (!missingArtist && missingCharter)
		{
			songRoleText.text = artistText;
		}
		else if (missingArtist && !missingCharter)
		{
			songRoleText.text = charterText;
		}
		else if (!missingArtist && !missingCharter)
		{
			songRoleText.text = artistText;

			final fadeTime:Float = 2;
			final startDelay:Float = 5;
			final loopDelay:Float = 5;

			FlxTween.tween(songRoleText, {alpha: 0}, fadeTime, {
				type: LOOPING,

				startDelay: loopDelay,
				loopDelay: loopDelay,

				onComplete: t ->
				{
					if (songRoleText.text == charterText)
						songRoleText.text = artistText;
					else
						songRoleText.text = charterText;

					FlxTween.tween(songRoleText, {alpha: 1}, fadeTime);
				},
			});
		}

		var startDelay:Float = 0.3;

		for (text in [levelInfo, levelDifficulty, deathCounter, songRoleText, practiceText])
		{
			text.alpha = 0;
			text.x = FlxG.width - (text.width + 20);

			FlxTween.tween(text, {alpha: 1, y: text.y + 5}, 0.4, {ease: FlxEase.quartInOut, startDelay: startDelay});
			startDelay += .2;
		}

		FlxTween.tween(bg, {alpha: 0.6}, 0.4, {ease: FlxEase.quartInOut});

		grpMenuShit = new FlxTypedGroup<Alphabet>();
		add(grpMenuShit);

		regenMenu();
	}

	private function regenMenu():Void
	{
		while (grpMenuShit.members.length > 0)
			grpMenuShit.remove(grpMenuShit.members[0], true);

		for (i in 0...menuItems.length)
		{
			var songText:Alphabet = new Alphabet(0, (70 * i) + 30, menuItems[i], true, false);
			songText.isMenuItem = true;
			songText.targetY = i;
			grpMenuShit.add(songText);
		}

		curSelected = 0;
		changeSelection();
	}

	override function update(elapsed:Float)
	{
		if (pauseMusic.volume < 0.5)
			pauseMusic.volume += 0.01 * elapsed;

		super.update(elapsed);

		songRoleText.x = FlxG.width - (songRoleText.width + 20);

		var upP = controls.UI_UP_P;
		var downP = controls.UI_DOWN_P;
		var accepted = controls.ACCEPT;

		if (upP)
			changeSelection(-1);
		if (downP)
			changeSelection(1);

		if (accepted)
		{
			var daSelected:String = menuItems[curSelected];

			switch (daSelected)
			{
				case "Resume":
					close();

				case "EASY" | 'NORMAL' | "HARD":
					PlayState.SONG = Song.loadFromJson(Highscore.formatSong(PlayState.SONG.song.toLowerCase(), curSelected), PlayState.SONG.song.toLowerCase());

					PlayState.storyDifficulty = curSelected;

					FlxG.switchState(() -> new PlayState());

				case 'Toggle Practice Mode':
					PlayState.practiceMode = !PlayState.practiceMode;
					practiceText.visible = PlayState.practiceMode;

				case 'Change Difficulty':
					menuItems = difficultyChoices;
					regenMenu();

				case 'BACK':
					menuItems = pauseOG;
					regenMenu();

				case "Restart Song":
					FlxG.switchState(() -> new PlayState());

				case "Exit to menu":
					PlayState.seenCutscene = false;
					PlayState.deathCounter = 0;

					// if (PlayState.isStoryMode)
					// 	FlxG.switchState(() -> new StoryMenuState());
					// else
					FlxG.switchState(() -> new FreeplayState());
			}
		}
	}

	override function destroy()
	{
		pauseMusic.destroy();

		super.destroy();
	}

	function changeSelection(change:Int = 0):Void
	{
		FlxG.sound.play(Paths.sound('scrollMenu'), 0.4);

		curSelected += change;

		if (curSelected < 0)
			curSelected = menuItems.length - 1;
		if (curSelected >= menuItems.length)
			curSelected = 0;

		var bullShit:Int = 0;

		for (item in grpMenuShit.members)
		{
			item.targetY = bullShit - curSelected;
			bullShit++;

			item.alpha = 0.6;
			// item.setGraphicSize(Std.int(item.width * 0.8));

			if (item.targetY == 0)
			{
				item.alpha = 1;
				// item.setGraphicSize(Std.int(item.width));
			}
		}
	}
}
